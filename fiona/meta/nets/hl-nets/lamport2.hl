SORT pid0 = [ 0 , 2 ] ;
     pid = [ 1 , 2 ] ;

message = RECORD
		a : pid;
		b : pid;
	  END;

    pidarray  = RECORD
			index : pid ;
 			value : BOOLEAN ;
		END ;

FUNCTION mess ( c : pid ; d : pid ) : message
VAR r : message ;
BEGIN
	r . a = c ;
	r . b = d ;
	RETURN r
END

FUNCTION cell ( a : pid ; b : BOOLEAN ) : pidarray
VAR r : pidarray ;
BEGIN
	r . index = a ;
	r . value = b ;
	RETURN r
END

FUNCTION allpid () : pid
VAR i : pid ;
BEGIN
	FOR ALL i DO RETURN i END
END

FUNCTION emptypid() : pid0 
BEGIN
RETURN 0
END

FUNCTION tt() : BOOLEAN
BEGIN
RETURN TRUE 
END

FUNCTION ff() : BOOLEAN
BEGIN
RETURN FALSE
END

PLACE
	start_1 : pid ,
	setx_3 : pid ,
	ify0_4 : pid ,
	sety_9 : pid ,
 	ifxi_10 : pid ,
	CS_19 : pid ,
	setbi_21 : pid ,
	yy : pid0,
	b : pidarray ,
	xx : pid0 ,
	setbi_5 : pid ,
	setbi_11 : pid ,
	fordo_12 : pid ,
	awaity : pid ,
	wait : message,
	done : message,
	await_13 : pid,
	ifyi_14 : pid ;
MARKING
	start_1 : allpid() ,
	xx : emptypid() ,
	yy : emptypid() ,
	b : cell ( allpid() , ff() ) ;

TRANSITION setbi_2
VAR i : pid ; bi : BOOLEAN ;
CONSUME start_1 : i , b : cell ( i , bi ) ;
PRODUCE b : cell ( i , tt() ) , setx_3 : i ;

TRANSITION setx_3
VAR i : pid ; x : pid0 ;
CONSUME setx_3 : i , xx : x ;
PRODUCE xx : i , ify0_4 : i ;

TRANSITION yeq0_4
VAR i : pid ;
CONSUME ify0_4 : i , yy : emptypid() ;
PRODUCE sety_9 : i , yy : emptypid() ;

TRANSITION sety_9
VAR i : pid ; y : pid0 ;
CONSUME yy : y , sety_9 : i ;
PRODUCE yy : i , ifxi_10 : i ;

TRANSITION xeqi_10
VAR i : pid ; 
CONSUME xx : i , ifxi_10 : i ;
PRODUCE CS_19 : i , xx : i ;

TRANSITION sety0_20
VAR i : pid ; y : pid0 ;
CONSUME CS_19 : i , yy : y ;
PRODUCE setbi_21 : i , yy : emptypid() ;

TRANSITION setbi_21
VAR i : pid ; bi : BOOLEAN ;
CONSUME b : cell ( i , bi ) , setbi_21 : i ;
PRODUCE b : cell ( i , ff() ) , start_1 : i ;

TRANSITION yne0_4
VAR i : pid ; y : pid0 ;
GUARD y > 0 
CONSUME yy : y , ify0_4 : i ;
PRODUCE yy : y , setbi_5 : i ;

TRANSITION xnei_10
VAR i : pid ; x : pid0 ;
GUARD x # i 
CONSUME xx : x , ifxi_10 : i ;
PRODUCE xx : x , setbi_11 : i ;

TRANSITION setbi_5
VAR i : pid ; bi : BOOLEAN ;
CONSUME setbi_5 : i , b : cell ( i , bi ) ;
PRODUCE awaity : i , b : cell ( i , ff() ) ;

TRANSITION setbi_11
VAR i : pid ; bi : BOOLEAN ;
CONSUME setbi_11 : i , b : cell ( i , bi ) ;
PRODUCE fordo_12 : i , b : cell ( i , ff() ) ;

TRANSITION fordo_12
VAR i : pid ;
CONSUME fordo_12 : i ;
PRODUCE wait : mess ( i , allpid() ), await_13 : i ;

TRANSITION await_13
VAR i , j : pid ;
CONSUME wait : mess ( i , j ) , b : cell ( j , ff() ) , await_13 : i;
PRODUCE b : cell ( j , ff() ) , done : mess ( i , j ) , await_13 : i;

TRANSITION forod_13
VAR i : pid ;
CONSUME await_13 : i ,  done : mess ( i, allpid() ) ;
PRODUCE ifyi_14 : i ;

TRANSITION awaity 
VAR i : pid ;
CONSUME awaity : i , yy : emptypid() ;
PRODUCE start_1 : i , yy : emptypid() ;

TRANSITION ynei_14 
VAR i : pid; y : pid0 ;
GUARD y # i
CONSUME ifyi_14 : i , yy : y ;
PRODUCE awaity : i , yy : y ;

TRANSITION yeqi_14
VAR i : pid ;
CONSUME yy : i , ifyi_14 : i ;
PRODUCE yy : i , CS_19 : i ;






	


 
