{Output Port des zweiten Wrappers}


{01: konkret}
{02: OR entfernt, weil im wrapper_01 auch OR im OP weggelassen.}
{    INT_CLK = LCLKM or REQ_INT}
{03: AND und beide D-FFs zusammenfassen; rst an D-FFs schaltet immer vor}
{     INT_CLK}
{04: Einzelner Pegelplatz kodiert nicht mehr Pegel für alle Signale, sondern}
{    jeweils zwei Plätze kodieren Pegel für ein Signal}


PLACE
    {Zusammengefasst: AND und beide D-FFs}
    op.and2ff.p.INT_CLK,
    op.and2ff.p.n_INT_CLK,
    op.and2ff.p.ACK_B,
    op.and2ff.p.n_ACK_B,
    op.and2ff.p.DATAV_OUT,
    op.and2ff.p.n_DATAV_OUT,
    {op.and2ff.p.DOV => op.oc.p.DOV}
    {op.and2ff.p.DONV => op.oc.p.DONV}
    op.and2ff.p.pegel_INT_CLK_0,
    op.and2ff.p.pegel_INT_CLK_1,
    op.and2ff.p.pegel_ACK_B_0,
    op.and2ff.p.pegel_ACK_B_1,
    op.and2ff.p.pegel_DATAV_OUT_0,
    op.and2ff.p.pegel_DATAV_OUT_1,
    op.and2ff.p.pegel_DOV_0,
    op.and2ff.p.pegel_DOV_1,
    op.and2ff.p.pegel_DONV_0,
    op.and2ff.p.pegel_DONV_1,

    {Output Controller im OP}
    op.oc.p.0,  {Zustände des Output Controllers}
    op.oc.p.1,
    op.oc.p.2,
    op.oc.p.3,
    op.oc.p.DOV,
    op.oc.p.n_DOV,
    op.oc.p.DONV,
    op.oc.p.n_DONV,
    op.oc.p.ACK_B,
    op.oc.p.n_ACK_B,
    op.oc.p.REQ_B,
    op.oc.p.n_REQ_B,
    op.oc.p.STRETCH,
    op.oc.p.n_STRETCH,
    op.oc.p.ACK_INT,
    op.oc.p.n_ACK_INT;


MARKING
    op.and2ff.p.n_INT_CLK: 1,
    op.and2ff.p.n_ACK_B: 1,
    op.and2ff.p.n_DATAV_OUT: 1,
    op.and2ff.p.pegel_INT_CLK_0: 1,
    op.and2ff.p.pegel_ACK_B_0: 1,
    op.and2ff.p.pegel_DATAV_OUT_0: 1,
    op.and2ff.p.pegel_DOV_0: 1,
    op.and2ff.p.pegel_DONV_0: 1,

    op.oc.p.0: 1,
    op.oc.p.n_DOV: 1,
    op.oc.p.n_DONV: 1,
    op.oc.p.n_ACK_B: 1,
    op.oc.p.n_REQ_B: 1,
    op.oc.p.n_STRETCH: 1,
    op.oc.p.n_ACK_INT: 1;



{Zusammengefasst: AND und beide D-FFs}

{Pegelkodierung: (INT_CLK, ACK_B, DATAV_OUT, DOV, DONV)}
TRANSITION op.and2ff.t.INT_CLK_pegel_1_x_x_0_1
CONSUME op.and2ff.p.INT_CLK: 1, op.and2ff.p.pegel_INT_CLK_1: 1,
        op.and2ff.p.pegel_DOV_0: 1, op.and2ff.p.pegel_DONV_1: 1,
        op.oc.p.n_DONV: 1;
PRODUCE op.and2ff.p.n_INT_CLK: 1, op.and2ff.p.pegel_INT_CLK_0: 1,
        op.and2ff.p.pegel_DOV_0: 1, op.and2ff.p.pegel_DONV_0: 1,
        op.oc.p.DONV: 1;

TRANSITION op.and2ff.t.INT_CLK_pegel_1_0_x_1_0
CONSUME op.and2ff.p.INT_CLK: 1, op.and2ff.p.pegel_INT_CLK_1: 1,
        op.and2ff.p.pegel_ACK_B_0: 1, op.and2ff.p.pegel_DOV_1: 1,
        op.and2ff.p.pegel_DONV_0: 1;
PRODUCE op.and2ff.p.n_INT_CLK: 1, op.and2ff.p.pegel_INT_CLK_0: 1,
        op.and2ff.p.pegel_ACK_B_0: 1, op.and2ff.p.pegel_DOV_1: 1,
        op.and2ff.p.pegel_DONV_0: 1;

TRANSITION op.and2ff.t.INT_CLK_pegel_1_1_x_1_0
CONSUME op.and2ff.p.INT_CLK: 1, op.and2ff.p.pegel_INT_CLK_1: 1,
        op.and2ff.p.pegel_ACK_B_1: 1, op.and2ff.p.pegel_DOV_1: 1,
        op.and2ff.p.pegel_DONV_0: 1, op.oc.p.n_DOV: 1;
PRODUCE op.and2ff.p.n_INT_CLK: 1, op.and2ff.p.pegel_INT_CLK_0: 1,
        op.and2ff.p.pegel_ACK_B_1: 1, op.and2ff.p.pegel_DOV_0: 1,
        op.and2ff.p.pegel_DONV_0: 1, op.oc.p.DOV: 1;

TRANSITION op.and2ff.t.INT_CLK_pegel_0_x_0_0_0
CONSUME op.and2ff.p.INT_CLK: 1, op.and2ff.p.pegel_INT_CLK_0: 1,
        op.and2ff.p.pegel_DATAV_OUT_0: 1, op.and2ff.p.pegel_DOV_0: 1,
        op.and2ff.p.pegel_DONV_0: 1, op.oc.p.n_DONV: 1;
PRODUCE op.and2ff.p.n_INT_CLK: 1, op.and2ff.p.pegel_INT_CLK_1: 1,
        op.and2ff.p.pegel_DATAV_OUT_0: 1, op.and2ff.p.pegel_DOV_0: 1,
        op.and2ff.p.pegel_DONV_1: 1, op.oc.p.DONV: 1;

TRANSITION op.and2ff.t.INT_CLK_pegel_0_x_0_1_0
CONSUME op.and2ff.p.INT_CLK: 1, op.and2ff.p.pegel_INT_CLK_0: 1,
        op.and2ff.p.pegel_DATAV_OUT_0: 1, op.and2ff.p.pegel_DOV_1: 1,
        op.and2ff.p.pegel_DONV_0: 1, op.oc.p.n_DOV: 1, op.oc.p.n_DONV: 1;
PRODUCE op.and2ff.p.n_INT_CLK: 1, op.and2ff.p.pegel_INT_CLK_1: 1,
        op.and2ff.p.pegel_DATAV_OUT_0: 1, op.and2ff.p.pegel_DOV_0: 1,
        op.and2ff.p.pegel_DONV_1: 1, op.oc.p.DOV: 1, op.oc.p.DONV: 1;

TRANSITION op.and2ff.t.INT_CLK_pegel_0_x_1_0_0
CONSUME op.and2ff.p.INT_CLK: 1, op.and2ff.p.pegel_INT_CLK_0: 1,
        op.and2ff.p.pegel_DATAV_OUT_1: 1, op.and2ff.p.pegel_DOV_0: 1,
        op.and2ff.p.pegel_DONV_0: 1, op.oc.p.n_DOV: 1;
PRODUCE op.and2ff.p.n_INT_CLK: 1, op.and2ff.p.pegel_INT_CLK_1: 1,
        op.and2ff.p.pegel_DATAV_OUT_1: 1, op.and2ff.p.pegel_DOV_1: 1,
        op.and2ff.p.pegel_DONV_0: 1, op.oc.p.DOV: 1;

TRANSITION op.and2ff.t.INT_CLK_pegel_0_x_1_1_0
CONSUME op.and2ff.p.INT_CLK: 1, op.and2ff.p.pegel_INT_CLK_0: 1,
        op.and2ff.p.pegel_DATAV_OUT_1: 1, op.and2ff.p.pegel_DOV_1: 1,
        op.and2ff.p.pegel_DONV_0: 1;
PRODUCE op.and2ff.p.n_INT_CLK: 1, op.and2ff.p.pegel_INT_CLK_1: 1,
        op.and2ff.p.pegel_DATAV_OUT_1: 1, op.and2ff.p.pegel_DOV_1: 1,
        op.and2ff.p.pegel_DONV_0: 1;


TRANSITION op.and2ff.t.ACK_B_pegel_x_1_x_x_x
CONSUME op.and2ff.p.ACK_B: 1, op.and2ff.p.pegel_ACK_B_1: 1;
PRODUCE op.and2ff.p.n_ACK_B: 1, op.and2ff.p.pegel_ACK_B_0: 1;

TRANSITION op.and2ff.t.ACK_B_pegel_1_0_x_x_x
CONSUME op.and2ff.p.ACK_B: 1, op.and2ff.p.pegel_INT_CLK_1: 1,
        op.and2ff.p.pegel_ACK_B_0: 1;
PRODUCE op.and2ff.p.n_ACK_B: 1, op.and2ff.p.pegel_INT_CLK_1: 1,
        op.and2ff.p.pegel_ACK_B_1: 1;

TRANSITION op.and2ff.t.ACK_B_pegel_0_0_x_0_x
CONSUME op.and2ff.p.ACK_B: 1, op.and2ff.p.pegel_INT_CLK_0: 1,
        op.and2ff.p.pegel_ACK_B_0: 1, op.and2ff.p.pegel_DOV_0: 1;
PRODUCE op.and2ff.p.n_ACK_B: 1, op.and2ff.p.pegel_INT_CLK_0: 1,
        op.and2ff.p.pegel_ACK_B_1: 1, op.and2ff.p.pegel_DOV_0: 1;

TRANSITION op.and2ff.t.ACK_B_pegel_0_0_x_1_x
CONSUME op.and2ff.p.ACK_B: 1, op.and2ff.p.pegel_INT_CLK_0: 1,
        op.and2ff.p.pegel_ACK_B_0: 1, op.and2ff.p.pegel_DOV_1: 1,
        op.oc.p.n_DOV: 1;
PRODUCE op.and2ff.p.n_ACK_B: 1, op.and2ff.p.pegel_INT_CLK_0: 1,
        op.and2ff.p.pegel_ACK_B_1: 1, op.and2ff.p.pegel_DOV_0: 1,
        op.oc.p.DOV: 1;


TRANSITION op.and2ff.t.DATAV_OUT_pegel_x_x_0_x_x
CONSUME op.and2ff.p.DATAV_OUT: 1, op.and2ff.p.pegel_DATAV_OUT_0: 1;
PRODUCE op.and2ff.p.n_DATAV_OUT: 1, op.and2ff.p.pegel_DATAV_OUT_1: 1;

TRANSITION op.and2ff.t.DATAV_OUT_pegel_x_x_1_x_x
CONSUME op.and2ff.p.DATAV_OUT: 1, op.and2ff.p.pegel_DATAV_OUT_1: 1;
PRODUCE op.and2ff.p.n_DATAV_OUT: 1, op.and2ff.p.pegel_DATAV_OUT_0: 1;



{Output Controller im OP}

TRANSITION op.oc.t.01
CONSUME op.oc.p.0: 1, op.oc.p.DOV: 1, op.oc.p.n_REQ_B: 1, op.oc.p.n_STRETCH: 1,
        op.oc.p.n_ACK_INT: 1;
PRODUCE op.oc.p.1: 1, op.oc.p.REQ_B: 1, op.oc.p.STRETCH: 1, op.oc.p.ACK_INT: 1,
        op.oc.p.n_DOV: 1;

TRANSITION op.oc.t.03
CONSUME op.oc.p.0: 1, op.oc.p.DONV: 1, op.oc.p.n_ACK_INT: 1;
PRODUCE op.oc.p.3: 1, op.oc.p.ACK_INT: 1, op.oc.p.n_DONV: 1;

TRANSITION op.oc.t.12
CONSUME op.oc.p.1: 1, op.oc.p.ACK_B: 1, op.oc.p.DOV: 1, op.oc.p.n_REQ_B: 1,
        op.oc.p.n_STRETCH: 1, op.oc.p.n_ACK_INT: 1;
PRODUCE op.oc.p.2: 1, op.oc.p.REQ_B: 1, op.oc.p.ACK_INT: 1, op.oc.p.STRETCH: 1,
        op.oc.p.n_ACK_B: 1, op.oc.p.n_DOV: 1;

TRANSITION op.oc.t.21
CONSUME op.oc.p.2: 1, op.oc.p.ACK_B: 1, op.oc.p.DOV: 1, op.oc.p.n_REQ_B: 1,
        op.oc.p.n_STRETCH: 1, op.oc.p.n_ACK_INT: 1;
PRODUCE op.oc.p.1: 1, op.oc.p.REQ_B: 1, op.oc.p.ACK_INT: 1, op.oc.p.STRETCH: 1,
        op.oc.p.n_ACK_B: 1, op.oc.p.n_DOV: 1;

TRANSITION op.oc.t.23
CONSUME op.oc.p.2: 1, op.oc.p.ACK_B: 1, op.oc.p.DONV: 1, op.oc.p.n_ACK_INT: 1;
PRODUCE op.oc.p.3: 1, op.oc.p.ACK_INT: 1, op.oc.p.n_ACK_B: 1, op.oc.p.n_DONV: 1;

TRANSITION op.oc.t.30
CONSUME op.oc.p.3: 1, op.oc.p.DONV: 1, op.oc.p.n_ACK_INT: 1;
PRODUCE op.oc.p.0: 1, op.oc.p.ACK_INT: 1, op.oc.p.n_DONV: 1;



{Anbindung an Aussenwelt}

{DATAV_OUT produzieren}
TRANSITION op.t.DATAV_OUT
CONSUME op.and2ff.p.n_DATAV_OUT: 1;
PRODUCE op.and2ff.p.DATAV_OUT: 1;

{INT_CLK produzieren}
TRANSITION op.t.INT_CLK
CONSUME op.and2ff.p.n_INT_CLK: 1;
PRODUCE op.and2ff.p.INT_CLK: 1;

{ACK_B produzieren}
TRANSITION op.t.ACK_B
CONSUME op.oc.p.n_ACK_B: 1, op.and2ff.p.n_ACK_B: 1;
PRODUCE op.oc.p.ACK_B: 1, op.and2ff.p.ACK_B: 1;

{ACK_INT abräumen}
TRANSITION op.t.ACK_INT
CONSUME op.oc.p.ACK_INT: 1;
PRODUCE op.oc.p.n_ACK_INT: 1;

{REQ_B abräumen}
TRANSITION op.t.REQ_B
CONSUME op.oc.p.REQ_B: 1;
PRODUCE op.oc.p.n_REQ_B: 1;

{STRETCH abräumen}
TRANSITION op.t.STRETCH
CONSUME op.oc.p.STRETCH: 1;
PRODUCE op.oc.p.n_STRETCH: 1;



{ vim: set ft=lola: }
